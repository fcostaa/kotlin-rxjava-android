apply plugin: 'com.android.feature'
apply plugin: 'kotlin-android'
apply plugin: 'kotlinx-serialization'
apply plugin: 'kotlin-kapt'
apply plugin: "com.vanniktech.dependency.graph.generator"

android {
    compileSdkVersion Versions.compileSdkVersion
    buildToolsVersion Versions.buildToolsVersion

    baseFeature true

    defaultConfig {
        minSdkVersion Versions.minSdkVersion
        targetSdkVersion Versions.targetSdkVersion
        versionCode Versions.versionCode
        versionName Versions.versionName

        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "MARVEL_PRIVATE_KEY", "\"" + MARVEL_PRIVATE_KEY + "\""
        buildConfigField "String", "MARVEL_PUBLIC_KEY", "\"" + MARVEL_PUBLIC_KEY + "\""
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        debugMini {
            initWith debug
            minifyEnabled true
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-rules-debug.pro'
            matchingFallbacks = ['debug']
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'proguard-rules-release.pro'
        }
    }

    lintOptions {
        // set to true to turn off analysis progress reporting by lint
        quiet false
        // if true, stop the gradle build if errors are found
        abortOnError true
        // if true, only report errors
        ignoreWarnings false
    }

    testOptions {
//        junitPlatform {
//            jupiterVersion "5.0.2"
//            vintageVersion "4.12.2"
//        }
        unitTests {
            includeAndroidResources = true
        }
    }
    packagingOptions {
        exclude 'META-INF/app_debug.kotlin_module'
        exclude 'META-INF/proguard/androidx-annotations.pro'
    }
}

repositories {
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://jitpack.io" }
}

kapt {
    correctErrorTypes = true
}

dependencies {
    application project(":apk")
    feature project(":feature:detail")
    feature project(":feature:listing")
    feature project(":feature:wiki")
    api project(':library:checkableheart')
    implementation project(':library:network')
    implementation project(':library:cache')

    api kotlinDependencies.values()

    api supportDependencies.values()
    api androidxDependencies.values()

    api daggerDependencies.values()
    kapt daggerCompilerDependencies.values()
    compileOnly annotationsDependencies.values()

    api imageLoaderDependencies.values()

    api rxJavaDependencies.values()

    api uiDependencies.values()

    api networkDependencies.values()
}

ext.add_test_dependencies(project)

task copyTestClasses(type: Copy) {
    from "build/tmp/kotlin-classes/debugUnitTest"
    into "build/intermediates/classes/debug"
}
