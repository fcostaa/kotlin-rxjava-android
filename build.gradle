apply from: 'gradleScript/dependencies.gradle'

buildscript {
    repositories {
        google()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://kotlin.bintray.com/kotlinx" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${Versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-serialization:${Versions.kotlin}"
        classpath "com.vanniktech:gradle-dependency-graph-generator-plugin:0.2.0"
        classpath "com.vanniktech:gradle-android-junit-jacoco-plugin:0.13.0"
    }
}

apply plugin: "com.vanniktech.android.junit.jacoco"

junitJacoco {
    jacocoVersion = "0.8.2"
    includeNoLocationClasses = true
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url "https://kotlin.bintray.com/kotlinx" }
    }
}

subprojects {
    ext {
        add_test_dependencies = { Project p ->
            p.dependencies {
                testImplementation project(':library:unit-test-utils')
                testImplementation kotlinTestDependencies.values()
                testImplementation networkTestDependencies.values()
                testImplementation testDependencies.values()

                androidTestImplementation networkTestDependencies.values()
                kaptAndroidTest daggerCompilerDependencies.values()
                androidTestImplementation androidTestDependencies.values()
                androidTestUtil androidTestUtilDependencies.values()
            }
        }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Adagger.gradle.incremental"
        }
    }

    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            android {
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }
            }
        }
    }
}
