apply from: 'gradleScript/dependencies.gradle'

buildscript {
    ext.kotlinVersion = '1.2.61'
    ext.kotlinSerializationVersion = '0.6.1'

    repositories {
        jcenter()
        google()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://kotlin.bintray.com/kotlinx" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlinx:kotlinx-gradle-serialization-plugin:$kotlinSerializationVersion"
        classpath "com.vanniktech:gradle-dependency-graph-generator-plugin:0.2.0"
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.3'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url "https://kotlin.bintray.com/kotlinx" }
    }
}

subprojects {
    ext {
        add_test_dependencies = {Project p ->
            p.dependencies {
                testImplementation project(':library:unit-test-utils')
                testImplementation kotlinTestDependencies.values()
                testImplementation networkTestDependencies.values()
                testImplementation testDependencies.values()

                androidTestImplementation networkTestDependencies.values()
                kaptAndroidTest daggerCompilerDependencies.values()
                androidTestImplementation androidTestDependencies.values()
                androidTestUtil androidTestUtilDependencies.values()
            }
        }
    }
}
