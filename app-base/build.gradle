apply plugin: 'com.android.feature'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: "com.vanniktech.dependency.graph.generator"

ext {
    travisBuild = System.getenv("TRAVIS") == "true"
    // allows for -Dpre-dex=false to be set
    preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))
}

android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version

    baseFeature = true

    defaultConfig {
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode 1
        versionName "1.0"

        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "MARVEL_PRIVATE_KEY", "\"" + MARVEL_PRIVATE_KEY + "\""
        buildConfigField "String", "MARVEL_PUBLIC_KEY", "\"" + MARVEL_PUBLIC_KEY + "\""
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !travisBuild
    }

    testOptions {
//        junitPlatform {
//            jupiterVersion "5.0.2"
//            vintageVersion "4.12.2"
//        }
        unitTests {
            includeAndroidResources = true
        }
    }
    packagingOptions {
        exclude 'META-INF/app_debug.kotlin_module'
    }
}

repositories {
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://jitpack.io" }
}

kapt {
    generateStubs = true
    correctErrorTypes = true
}

dependencies {
    implementation project(':checkableheart')
    application project(":app-apk")
    feature project(":app-detail")
    feature project(":app-listing")
    feature project(":app-wiki")

    implementation kotlinDependencies.values()

    implementation supportDependencies.values()
    implementation androidxDependencies.values()

    implementation daggerDependencies.values()
    kapt daggerCompilerDependencies.values()
    compileOnly annotationsDependencies.values()

    implementation imageLoaderDependencies.values()

    implementation rxJavaDependencies.values()

    implementation uiDependencies.galleryLayoutManager

    implementation networkDependencies.values()

    implementation lruCacheDependencies.lruCache

    testImplementation kotlinTestDependencies.values()
    testImplementation networkTestDependencies.values()
    testImplementation testDependencies.values()

    androidTestImplementation networkTestDependencies.values()
    kaptAndroidTest daggerCompilerDependencies.values()
    androidTestImplementation androidTestDependencies.values()
    androidTestUtil androidTestUtilDependencies.values()
}

task copyTestClasses(type: Copy) {
    from "build/tmp/kotlin-classes/debugUnitTest"
    into "build/intermediates/classes/debug"
}
