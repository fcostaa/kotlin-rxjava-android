apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'de.mannodermaus.android-junit5'
apply plugin: 'jacoco-android'

ext {
    travisBuild = System.getenv("TRAVIS") == "true"
    // allows for -Dpre-dex=false to be set
    preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))
}

android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version

    defaultConfig {
        applicationId "com.felipecosta.kotlinrxjavasample"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "com.felipecosta.kotlinrxjavasample.runner.CustomTestRunner"

        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "MARVEL_PRIVATE_KEY", "\"" + MARVEL_PRIVATE_KEY + "\""
        buildConfigField "String", "MARVEL_PUBLIC_KEY", "\"" + MARVEL_PUBLIC_KEY + "\""
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !travisBuild
    }

    testOptions {
        junitPlatform {
            jupiterVersion "5.0.2"
            vintageVersion "4.12.2"
        }
    }
}

jacocoAndroidUnitTestReport {
    excludes += [
            '**/*Module.*', // Modules for Dagger.
            '**/*Dagger*.*', // Dagger auto-generated code.
            '**/*MembersInjector*.*', // Dagger auto-generated code.
            '**/*_Provide*Factory*.*',
            '**/*_Factory.*', //Dagger auto-generated code
            '**/*$*$*.*'
    ]

    csv.enabled false
    html.enabled true
    xml.enabled true
}

repositories {
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://jitpack.io" }
}

kapt {
    generateStubs = true
    correctErrorTypes = true
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation "com.android.support:appcompat-v7:$support_library_version"
    implementation "com.android.support:design:$support_library_version"
    implementation "com.android.support:support-v4:$support_library_version"
    implementation "com.android.support:recyclerview-v7:$support_library_version"
    implementation "com.android.support:cardview-v7:$support_library_version"

    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"
    compileOnly "javax.annotation:jsr250-api:$javax_annotation_version"

    implementation "com.nostra13.universalimageloader:universal-image-loader:$universal_image_loader_version"

    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rxjava_android_version"
    implementation "com.jakewharton.rxrelay2:rxrelay:$rxjava_jakewharton_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$rxjava_jakewharton_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-design-kotlin:$rxjava_jakewharton_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:$rxjava_jakewharton_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7-kotlin:$rxjava_jakewharton_version"

    implementation 'com.github.felipehjcosta:rxaction:1.1.0'
    implementation 'com.github.felipehjcosta:gallerylayoutmanager:1.1.0'

    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "com.google.code.gson:gson:$gson_version"

    implementation 'com.jakewharton:disklrucache:2.0.2'

    implementation project(':checkableheart')

    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"
    testImplementation "io.mockk:mockk:1.6"

    testImplementation 'org.mockito:mockito-core:2.6.3'
    testImplementation("com.nhaarman:mockito-kotlin:1.3.0") {
        exclude group: 'org.jetbrains.kotlin'
    }

    testImplementation junit5()
    // (Optional) For running tests inside Android Studio 3.x (see below for details)
    testCompileOnly junit5EmbeddedRuntime()


    androidTestImplementation 'org.mockito:mockito-android:2.6.3'
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"
    androidTestImplementation 'com.squareup.rx.idler:rx2-idler:0.9.0'
    kaptAndroidTest "com.google.dagger:dagger-compiler:$dagger_version"
    kaptAndroidTest "com.google.dagger:dagger-android-processor:$dagger_version"

    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.1') {
        exclude group: 'com.android.support'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }

    androidTestImplementation('com.android.support.test.espresso:espresso-contrib:3.0.1') {
        exclude group: 'com.android.support'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }
    androidTestImplementation 'com.linkedin.testbutler:test-butler-library:1.3.1'
    androidTestUtil 'com.linkedin.testbutler:test-butler-app:1.3.0@apk'
}

task copyTestClasses(type: Copy) {
    from "build/tmp/kotlin-classes/debugUnitTest"
    into "build/intermediates/classes/debug"
}